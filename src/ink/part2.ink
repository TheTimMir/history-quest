VAR score2 = 0
VAR story2_completed = false

=== part2 ===
# lazyload bundle bg baptism dnipro

-> scene_pagan_kyiv

=== scene_pagan_kyiv ===
# show image kyivPaganHills with dissolve

narrator: Київ стоїть на високих пагорбах. Над містом — дерев'яні ідоли Перуна, Дажбога й Мокоші. 

narrator: Процесії йдуть до святилищ, несуть жертви, складають присяги, співають гімни давнім богам.

kyivite: Так було завжди… Дим жертовників змішується з туманом Дніпра.

narrator: Та невидимо наближається зміна звичного ладу днів.

-> scene_court

=== scene_court ===
# show image volodymyrHall x -100 y 0 with dissolve duration 1

narrator: У княжому теремі Володимир слухає послів із різних країв.

rabbi: Княже, Закон Мойсея навчає справедливості та заповідям.

imam: Віра ісламу — сила єдності та покори Єдиному.

monk: Христова віра — милосердя, світло й спасіння.

volodymyr: Я зважую: що принесе Русі мир і лад, що піднесе людей духом і навчить жити інакше?

...
narrator: Довга рада стишується.

volodymyr: Сподобалася нам віра грецька.

-> scene_idols

=== scene_idols ===
# show image perunDragged with dissolve

narrator: По місту покотилася чутка: ідолів знесуть.

warrior: Тягніть, братці!

kyivite: Ох, литі боги…

kyivite: Хтось кидає у воду квіти, хтось мовчить, стискаючи кулаки.

narrator: Довго плив ідол проти течії — ніби давня Русь упирається.
<>А тоді вода забрала його, і на березі стало тихо, як перед сходом сонця.

+ [До Дніпра]
    -> scene_baptism

=== scene_baptism ===
# show image dniproBaptism with dissolve duration 4

narrator: На ранок лунав дзвін. Люди виходили до Дніпра цілими родинами.

priest: В ім’я Отця, і Сина, і Святого Духа.

volodymyr: Ступайте у хвилі. Хай омиє вас не лише вода, а й надія.

child: Тату, вода тепла!

warrior: Знімаю зброю — і входжу до води.

narrator: Сонце торкається поверхні. Обличчя сяють чистотою.

+ [Після хрещення]
    -> scene_after

=== scene_after ===
# show image scriptorium with dissolve

narrator: У місто прийшли нові клопоти — і нове життя.

scribe: При світлі свічок переписуємо книги в монастирських келіях.

priest: При церквах відкриваємо школи — діти виводять перші літери.

narrator: На пергаменті з’являються рівні рядки новим письмом — кирилицею. Освіта прищеплює людям не лише знання, а й іншу мову серця.

volodymyr: Київ, омитий Дніпром і словом, входить у нову епоху.

+ [Перевірка знань]
    -> quiz2

+ [Завершити історію без тесту]
    -> outro2

=== quiz2 ===
narrator: Хто стояв у вигляді ідолів на київських пагорбах до хрещення?

*   [Перун, Дажбог, Мокош] 
    ~ score2 += 1
    narrator: Вірно.
    -> q2q2
*   [Хорс, Велес, Стрибог]
    narrator: Ні, у нашій сцені названі інші імена.
    -> q2q2
*   [Юпітер, Марс, Венера]
    narrator: Це античні боги, не язичницькі божества Русі.
    -> q2q2
*   [Сварог, Ярило, Лада]
    narrator: У тексті згадані інші ідоли.
    -> q2q2

=== q2q2 ===
narrator: Яке рішення ухвалив князь Володимир після розмов із послами різних віровчень?

*   [«Сподобалася нам віра грецька» — обрати християнство]
    ~ score2 += 1
    narrator: Саме так.
    -> q2q3
*   [Прийняти іслам]
    narrator: Ні, у нашій історії обрано інше віровчення.
    -> q2q3
*   [Залишити язичництво]
    narrator: Навпаки — ідолів знесли.
    -> q2q3
*   [Запровадити релігійну рівність без змін]
    narrator: У літописній традиції йдеться про хрещення Русі.
    -> q2q3

=== q2q3 ===
narrator: Які зміни описані після хрещення в Дніпрі?

*   [Відкриття шкіл при церквах, переписування книг, поява кирилиці]
    ~ score2 += 1
    narrator: Вірно.
    -> result2
*   [Закриття бібліотек і шкіл]
    narrator: Навпаки, освіта поширюється.
    -> result2
*   [Заборона письма і навчання]
    narrator: Ні, якраз з’являється нове письмо.
    -> result2
*   [Переселення киян із правого берега]
    narrator: Такого ми не читали в оповіді.
    -> result2

=== result2 ===
# call save_score
narrator: Ваш результат: {score2}/3.

+ [Повторити тест]
    ~ score2 = 0
    -> quiz2

+ [Завершити]
    -> outro2

=== outro2 ===
# show image bg background_kyiv_sunset

narrator: Так Київ, омитий Дніпром і словом, увійшов у нову епоху. 

~ story2_completed = true
-> part3
